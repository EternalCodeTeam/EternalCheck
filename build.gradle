plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group "com.eternalcode.check"
version "1.0.0"

repositories {
    gradlePluginPortal()
    mavenCentral()
    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url = "https://repo.panda-lang.org/releases" }
}

dependencies {
    // lombok
    compileOnly "org.projectlombok:lombok:1.18.22"
    annotationProcessor "org.projectlombok:lombok:1.18.22"

    // annotations for plugins
    compileOnly "org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT"
    annotationProcessor "org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT"

    // spigot api
    compileOnly "org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT"

    // LiteCommands
    implementation "dev.rollczi.litecommands:bukkit:1.8.4"

    // cdn configs
    implementation "net.dzikoysk:cdn:1.13.10"

    // expressible
    implementation "org.panda-lang:expressible:1.1.17"

    // adventure
    implementation "net.kyori:adventure-platform-bukkit:4.1.0"
}

compileJava.options.encoding = "UTF-8"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveFileName = "etrlCheck v${project.version}.jar"

    exclude "org/intellij/lang/annotations/**"
    exclude "org/jetbrains/annotations/**"
    exclude "javax/**"

    relocate "net.dzikoysk", "com.eternalcode.check.libs.net.dzikoysk"
    relocate "dev.rollczi", "com.eternalcode.check.libs.dev.rollczi"
    relocate "org.panda_lang", "com.eternalcode.check.libs.panda"
    relocate "net.kyori", "com.eternalcore.check.libs.net.kyori"
    relocate "panda", "com.eternalcode.check.libs.panda"
}